# This module allows you to generate a file with basic authentication credentials for use with Apache Webserver ea
class basicauth (
  String[1]                $owner       = 'www-data',
  String[1]                $group       = 'www-data',
  Stdlib::Filemode         $mode        = '0600',
  Enum['present','absent'] $ensure      = 'absent', # Do not do anything unless explicitly enabled
  Stdlib::Unixpath         $location    = '/var/www/.basicauth',
  Hash[String[1], Hash]    $basic_entry = {},
) {

  if $ensure == 'present' {

    concat { $location:
      owner => $owner,
      group => $group,
      mode  => $mode,
    }
    concat::fragment { 'file-header':
      target  => $location,
      content => "#This file is generated by Puppet, do not change manually!\n",
      order   => '01',
    }
    $basic_entry.each |String $entry_title, Hash $entry_attributes| {
      basicauth::basic_entry { $entry_title:
        * => $entry_attributes,
      }
    }

  } elsif $ensure == 'absent' {

    file { $location:
      ensure => 'absent',
      force  => true,
    }

  }

}
